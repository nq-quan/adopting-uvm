diff --git a/verif/alutb/alu_wrapper.sv b/verif/alutb/alu_wrapper.sv
index 14950c1..994a9e4 100755
--- a/verif/alutb/alu_wrapper.sv
+++ b/verif/alutb/alu_wrapper.sv
@@ -13,7 +13,8 @@
 
 module alu_wrapper(input logic tb_clk,
                    tb_rst_n,
-                   ctx_intf ctx_i);
+                   ctx_intf ctx_i,
+						 alu_intf alu_i);
 
    reg                  alu_ctl;
    reg [7:0]            alu_dat;
@@ -32,11 +33,11 @@ module alu_wrapper(input logic tb_clk,
            .frame                       (frame),
            .frame_bp                    (frame_bp),
            .frame_data                  (frame_data[31:0]),
-           .alu_ready                   (alu_ready),
-           .alu_result                  (alu_result[31:0]),
+           .alu_ready                   (alu_i.ready),
+           .alu_result                  (alu_i.result[31:0]),
            // Inputs
-           .alu_ctl                     (alu_ctl),
-           .alu_dat                     (alu_dat[7:0]),
+           .alu_ctl                     (alu_i.ctl),
+           .alu_dat                     (alu_i.dat[7:0]),
            .clk                         (tb_clk),
            .ctx_in                      (ctx_i.in[7:0]),
            .ctx_val                     (ctx_i.val),
diff --git a/verif/alutb/alutb.flist b/verif/alutb/alutb.flist
index 59fb1e8..271cb09 100755
--- a/verif/alutb/alutb.flist
+++ b/verif/alutb/alutb.flist
@@ -8,5 +8,7 @@
 ../../verif/alutb/alu_wrapper.sv
 ../../verif/alutb/tests/basic.sv
 ../../verif/alutb/tests/base_test.sv
+../../verif/alutb/tests/fast_clk.sv
+../../verif/alutb/tests/duty_cycle.sv
 -f ../../verif/alutb/rtl.flist
 
diff --git a/verif/alutb/alutb_tb_top.sv b/verif/alutb/alutb_tb_top.sv
index a9a5bfb..e3a80d9 100755
--- a/verif/alutb/alutb_tb_top.sv
+++ b/verif/alutb/alutb_tb_top.sv
@@ -33,13 +33,19 @@ module alutb_tb_top;
    // CTX Interface
    ctx_intf ctx_i(.clk(tb_clk), .rst_n(tb_rst_n));
 
+   // field: alu_i
+   // The <alu_intf> instance
+   alu_intf alu_i(.clk(tb_clk), .rst_n(tb_rst_n));
+   initial
+      alu_i.reset();
+
    //----------------------------------------------------------------------------------------
    // Group: DUT
 
    // obj: dut_wrapper
-   alu_wrapper alu_wrapper(/*AUTOINST*/
-                           // Interfaces
+   alu_wrapper alu_wrapper(// Interfaces
                            .ctx_i               (ctx_i),
+                           .alu_i               (alu_i),
                            // Inputs
                            .tb_clk              (tb_clk),
                            .tb_rst_n            (tb_rst_n));
@@ -51,10 +57,12 @@ module alutb_tb_top;
    // func: pre_run_test
    // Set interface names before run_test is called
    function void pre_run_test();
-      `cn_set_intf(virtual cn_clk_intf    , "cn_pkg::clk_intf"  , "tb_clk_vi", tb_clk_i);
-      `cn_set_intf(virtual cn_rst_intf    , "cn_pkg::rst_intf"  , "tb_rst_vi", tb_rst_i);
-      `cn_set_intf(virtual ctx_intf.drv_mp, "ctx_pkg::ctx_intf" , "ctx_vi"   , ctx_i.drv_mp);
-      `cn_set_intf(virtual ctx_intf.mon_mp, "ctx_pkg::ctx_intf" , "ctx_vi"   , ctx_i.mon_mp);
+      `cn_set_intf(virtual cn_clk_intf    , "cn_pkg::clk_intf"  , "tb_clk_vi", tb_clk_i)
+      `cn_set_intf(virtual cn_rst_intf    , "cn_pkg::rst_intf"  , "tb_rst_vi", tb_rst_i)
+      `cn_set_intf(virtual ctx_intf.drv_mp, "ctx_pkg::ctx_intf" , "ctx_vi"   , ctx_i.drv_mp)
+      `cn_set_intf(virtual ctx_intf.mon_mp, "ctx_pkg::ctx_intf" , "ctx_vi"   , ctx_i.mon_mp)
+      `cn_set_intf(virtual alu_intf.drv_mp, "alu_pkg::alu_intf" , "drv_vi"   , alu_i.drv_mp)
+      `cn_set_intf(virtual alu_intf.mon_mp, "alu_pkg::alu_intf" , "mon_vi"   , alu_i.mon_mp)
    endfunction : pre_run_test
 
    `include "tb_common.v"
diff --git a/verif/alutb/clk_duty_cycle.sv b/verif/alutb/clk_duty_cycle.sv
new file mode 100644
index 0000000..937e0f6
--- /dev/null
+++ b/verif/alutb/clk_duty_cycle.sv
@@ -0,0 +1,49 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   clk_duty_cycle.sv
+// Author: bhunter
+/* About:  <description>
+ *************************************************************************/
+
+`ifndef __CLK_DUTY_CYCLE_SV__
+   `define __CLK_DUTY_CYCLE_SV__
+
+// class: clk_duty_cycle_c
+// A clock with a duty cycle of 75/25.
+class clk_duty_cycle_c extends cn_pkg::clk_drv_c;
+   `uvm_component_utils(clk_duty_cycle_c)
+
+   //----------------------------------------------------------------------------------------
+   // Group: Methods
+   function new(string name="clk_duty_cycle",
+                uvm_component parent=null);
+      super.new(name, parent);
+   endfunction : new
+
+   ////////////////////////////////////////////
+   // func: run_phase
+   virtual task run_phase(uvm_phase phase);
+      int uptime = 3*period_ps / 4;
+      int downtime = period_ps - uptime;
+
+      // set to initial value
+      clk_vi.clk = (init_x)? 'bx : init_value;
+      #(init_delay_ps * 1ps);
+
+      forever begin
+         clk_vi.clk = 1;
+         #(uptime * 1ps);
+         clk_vi.clk = 0;
+         #(downtime * 1ps);
+      end
+   endtask : run_phase
+
+endclass : clk_duty_cycle_c
+
+`endif // __CLK_DUTY_CYCLE_SV__
diff --git a/verif/alutb/tb.py b/verif/alutb/tb.py
index 3adcb12..78054b1 100755
--- a/verif/alutb/tb.py
+++ b/verif/alutb/tb.py
@@ -2,7 +2,7 @@
 VKITS = ['cn', 'global', 'csr', 'ctx', 'alutb']
 
 # Add testbench flists
-FLISTS = ['alutb.flist', 'rtl.flist']
+FLISTS = ['../../verif/vkits/alu/alu.flist', 'alutb.flist', 'rtl.flist']
 
 # Testbench Top-level
 TB_TOP = 'alutb_tb_top'
diff --git a/verif/alutb/tests/basic.sv b/verif/alutb/tests/basic.sv
index ab5848d..1fd1471 100755
--- a/verif/alutb/tests/basic.sv
+++ b/verif/alutb/tests/basic.sv
@@ -16,23 +16,39 @@
 
    `include "base_test.sv"
 
-// class: basic_test_c
-// A basic ALU test
 class basic_test_c extends base_test_c;
    `uvm_component_utils(basic_test_c)
 
    //----------------------------------------------------------------------------------------
-   // Methods
+   // Group: Methods
    function new(string name="test",
                 uvm_component parent=null);
       super.new(name, parent);
    endfunction : new
 
    ////////////////////////////////////////////
-   // func: build_phase
-   virtual function void build_phase(uvm_phase phase);
-      super.build_phase(phase);
-   endfunction : build_phase
+   // func: main_phase
+   virtual task main_phase(uvm_phase phase);
+      byte unsigned stream[];
+      alu_pkg::item_c item;
+      alu_pkg::item_c unp_item = alu_pkg::item_c::type_id::create("unp_item");;
+
+      phase.raise_objection(this);
+
+      repeat(50) begin
+         item = alu_pkg::item_c::type_id::create("item");
+         item.randomize();
+         `cn_info(("Created ALU transaction: %s", item.convert2string()))
+         item.pack_bytes(stream);
+         `cn_info(("Bytes: %s", cn_pkg::print_ubyte_array(stream)))
+         unp_item.unpack_bytes(stream);
+         `cn_info(("Unpacked: %s", unp_item.convert2string()))
+         if(item.compare(unp_item) == 0)
+            `cn_err(("Miscompare!"))
+      end
+
+      phase.drop_objection(this);
+   endtask : main_phase
 
 endclass : basic_test_c
 
diff --git a/verif/alutb/tests/duty_cycle.sv b/verif/alutb/tests/duty_cycle.sv
new file mode 100644
index 0000000..b6bd0c6
--- /dev/null
+++ b/verif/alutb/tests/duty_cycle.sv
@@ -0,0 +1,42 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   duty_cycle.sv
+// Author: bhunter
+/* About:  <description>
+ *************************************************************************/
+
+`ifndef __DUTY_CYCLE_SV__
+   `define __DUTY_CYCLE_SV__
+
+   `include "base_test.sv"
+   `include "clk_duty_cycle.sv"
+
+// class: duty_cycle_test_c
+// Run using the clk_duty_cycle_c instead of cn_pkg::clk_drv_c
+class duty_cycle_test_c extends base_test_c;
+   `uvm_component_utils(duty_cycle_test_c)
+
+   //----------------------------------------------------------------------------------------
+   // Group: Methods
+   function new(string name="duty_cycle_test",
+                uvm_component parent=null);
+      super.new(name, parent);
+   endfunction : new
+
+   ////////////////////////////////////////////
+   // func: build_phase
+   virtual function void build_phase(uvm_phase phase);
+      set_type_override_by_type(cn_pkg::clk_drv_c::get_type(),
+                                clk_duty_cycle_c::get_type(), .replace(1));
+      super.build_phase(phase);
+   endfunction : build_phase
+
+endclass : duty_cycle_test_c
+
+`endif // __DUTY_CYCLE_SV__
diff --git a/verif/alutb/tests/fast_clk.sv b/verif/alutb/tests/fast_clk.sv
new file mode 100644
index 0000000..d226acd
--- /dev/null
+++ b/verif/alutb/tests/fast_clk.sv
@@ -0,0 +1,69 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   fast_clk.sv
+// Author: bhunter
+/* About:  <description>
+ *************************************************************************/
+
+`ifndef __FAST_CLK_SV__
+   `define __FAST_CLK_SV__
+
+   `include "basic.sv"
+// (`includes go here)
+
+// class: fast_clk_test_c
+// (Describe me)
+class fast_clk_test_c extends base_test_c;
+   `uvm_component_utils_begin(fast_clk_test_c)
+   `uvm_component_utils_end
+
+   //----------------------------------------------------------------------------------------
+   // Group: Configuration Fields
+
+   //----------------------------------------------------------------------------------------
+   // Group: Fields
+
+   //----------------------------------------------------------------------------------------
+   // Group: Methods
+   function new(string name="fast_clk",
+                uvm_component parent=null);
+      super.new(name, parent);
+   endfunction : new
+
+   ////////////////////////////////////////////
+   // func: build_phase
+   virtual function void build_phase(uvm_phase phase);
+      super.build_phase(phase);
+   endfunction : build_phase
+
+   ////////////////////////////////////////////
+   // func: connect_phase
+   virtual function void connect_phase(uvm_phase phase);
+      super.connect_phase(phase);
+   endfunction : connect_phase
+
+   ////////////////////////////////////////////
+   // func: end_of_elaboration_phase
+   virtual function void end_of_elaboration_phase(uvm_phase phase);
+      super.end_of_elaboration_phase(phase);
+      tb_clk_drv.randomize(period_ps) with {
+         period_ps inside {[1800:1999]};
+      };
+      `cn_info(("Selected a period of %0dps", tb_clk_drv.period_ps))
+   endfunction : end_of_elaboration_phase
+
+   ////////////////////////////////////////////
+   // func: run_phase
+   virtual task run_phase(uvm_phase phase);
+      super.run_phase(phase);
+   endtask : run_phase
+
+endclass : fast_clk_test_c
+
+`endif // __FAST_CLK_SV__
diff --git a/verif/vkits/alu/alu.flist b/verif/vkits/alu/alu.flist
new file mode 100644
index 0000000..a67307d
--- /dev/null
+++ b/verif/vkits/alu/alu.flist
@@ -0,0 +1,3 @@
++incdir+../../verif/vkits/alu
+../../verif/vkits/alu/alu_intf.sv
+../../verif/vkits/alu/alu_pkg.sv
diff --git a/verif/vkits/alu/alu_intf.sv b/verif/vkits/alu/alu_intf.sv
new file mode 100644
index 0000000..e9301f8
--- /dev/null
+++ b/verif/vkits/alu/alu_intf.sv
@@ -0,0 +1,109 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   alu_intf.sv
+// Author: bhunter
+/* About:  <description>
+ *************************************************************************/
+
+`ifndef __ALU_INTF_SV__
+   `define __ALU_INTF_SV__
+
+interface alu_intf(input logic clk,
+                   input logic rst_n);
+   import uvm_pkg::*;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Signals
+
+   // var: ctl
+   // Asserted only on the first cycle of a new transaction, while dat contains the operation
+   logic        ctl;
+
+   // var: dat
+   // The input data nibble that contains the operation and operands
+   logic [7:0]  dat;
+
+   // var: ready
+   // The output of the ALU that indicates when the result data is valid
+   logic        ready;
+
+   // var: result
+   // The 32-bit result data
+   logic [31:0] result;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Clocking blocks
+
+   // var: drv_cb
+   // A clocking block that represents how the environment's driver sees the interface
+   clocking drv_cb @(posedge clk);
+      output    ctl;
+      output    dat;
+      input     ready;
+      input     result;
+      input     rst_n;
+   endclocking : drv_cb
+
+   // var: mon_cb
+   // A clocking block that represents how the environment's monitor sees the interface
+   clocking mon_cb @(posedge clk);
+      input     ctl;
+      input     dat;
+      input     ready;
+      input     result;
+      input     rst_n;
+   endclocking : mon_cb
+
+   //----------------------------------------------------------------------------------------
+   // Group: Modports
+
+   modport drv_mp(clocking drv_cb,
+                  import reset);
+   modport mon_mp(clocking mon_cb);
+
+   //----------------------------------------------------------------------------------------
+   // Group: Methods
+
+   // func: reset
+   // Convenience function for the driver to reset its outputs
+   function void reset();
+      ctl = 0;
+      dat = 8'b0;
+   endfunction : reset
+
+   //----------------------------------------------------------------------------------------
+   // Group: Assertions
+
+   ctl_not_x :
+      assert property(@(posedge clk)
+                      disable iff (~rst_n || rst_n === 1'bx)
+                      (!$isunknown(ctl))) else
+         `cn_err_intf(("ctl signal is an X"))
+
+   dat_not_x :
+      assert property(@(posedge clk)
+                      disable iff (~rst_n || rst_n === 1'bx)
+                      (!$isunknown(dat))) else
+         `cn_err_intf(("dat signal is an X"))
+
+   ready_not_x :
+      assert property(@(posedge clk)
+                      disable iff (~rst_n || rst_n === 1'bx)
+                      (!$isunknown(ready))) else
+         `cn_err_intf(("ready signal is an X"))
+
+   result_not_x :
+      assert property(@(posedge clk)
+                      disable iff (~rst_n || rst_n === 1'bx || ready == 0)
+                      (!$isunknown(result))) else
+         `cn_err_intf(("result signal is an X"))
+
+endinterface : alu_intf
+
+`endif // __ALU_INTF_SV__
diff --git a/verif/vkits/alu/alu_item.sv b/verif/vkits/alu/alu_item.sv
new file mode 100644
index 0000000..261082c
--- /dev/null
+++ b/verif/vkits/alu/alu_item.sv
@@ -0,0 +1,118 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   alu_item.sv
+// Author: bhunter
+/* About:  <description>
+ *************************************************************************/
+
+`ifndef __ALU_ITEM_SV__
+   `define __ALU_ITEM_SV__
+
+// class: item_c
+// An ALU Transaction as a sequence item
+class item_c extends uvm_sequence_item;
+   //----------------------------------------------------------------------------------------
+   // Group: Types
+   typedef enum bit [7:0] {
+                           ADD_A_B = 0,
+                           SUB_A_B = 1,
+                           SUB_B_A = 2,
+                           MUL_A_B = 3,
+                           DIV_A_B = 4,
+                           DIV_B_A = 5,
+                           INC_A   = 6,
+                           INC_B   = 7,
+                           CLR_RES = 8,
+                           ACCUM   = 9
+                           } operation_e;
+
+   `uvm_object_utils_begin(alu_pkg::item_c)
+      `uvm_field_enum(operation_e, operation, UVM_ALL_ON)
+      `uvm_field_int (alpha,                  UVM_ALL_ON | UVM_NOPACK | UVM_NOCOMPARE)
+      `uvm_field_int (beta,                   UVM_ALL_ON | UVM_NOPACK | UVM_NOCOMPARE)
+   `uvm_object_utils_end
+
+   //----------------------------------------------------------------------------------------
+   // Group: Fields
+
+   // field: operation
+   rand operation_e operation;
+
+   // field: A variable
+   rand bit [15:0] alpha;
+
+   // field: B variable
+   rand bit [15:0] beta;
+
+   // ensure that all operands have legal values
+   constraint protocol_cnstr {
+      (operation == DIV_A_B) -> beta != 0;
+      (operation == DIV_B_A) -> alpha != 0;
+      (operation == SUB_A_B) -> alpha > beta;
+      (operation == SUB_B_A) -> beta > alpha;
+   }
+
+   // field: result
+   // This is the result of the operation, filled in by the driver and sent back with the response
+   result_t result;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Methods
+   function new(string name="item");
+      super.new(name);
+   endfunction : new
+
+   //----------------------------------------------------------------------------------------
+   // func: convert2string
+   // Single-line printing
+   virtual function string convert2string();
+      convert2string = $sformatf("%s A:%04X B:%04X", operation, alpha, beta);
+   endfunction : convert2string
+
+   ////////////////////////////////////////////
+   // func: do_pack
+   virtual function void do_pack(uvm_packer packer);
+      super.do_pack(packer);
+
+      if(operation inside {[ADD_A_B : INC_A], ACCUM})
+         packer.pack_field_int(alpha, 16);
+      if(operation inside {[ADD_A_B : DIV_B_A], INC_B})
+         packer.pack_field_int(beta, 16);
+   endfunction : do_pack
+
+   ////////////////////////////////////////////
+   // func: do_unpack
+   virtual function void do_unpack(uvm_packer packer);
+      super.do_unpack(packer);
+
+      if(operation inside {[ADD_A_B : INC_A], ACCUM})
+         alpha = packer.unpack_field_int(16);
+      if(operation inside {[ADD_A_B : DIV_B_A], INC_B})
+         beta = packer.unpack_field_int(16);
+   endfunction : do_unpack
+
+   ////////////////////////////////////////////
+   // func: do_compare
+   virtual function bit do_compare(uvm_object rhs,
+                                   uvm_comparer comparer);
+      item_c _rhs;
+      $cast(_rhs, rhs);
+      do_compare = super.do_compare(rhs, comparer);
+      if(operation inside {[ADD_A_B : INC_A], ACCUM})
+         do_compare &= comparer.compare_field_int("alpha", alpha, _rhs.alpha, 16);
+      if(operation inside {[ADD_A_B : DIV_B_A], INC_B})
+         do_compare &= comparer.compare_field_int("beta", beta, _rhs.beta, 16);
+   endfunction : do_compare
+
+endclass : item_c
+
+
+
+`endif // __ALU_ITEM_SV__
+
diff --git a/verif/vkits/alu/alu_pkg.sv b/verif/vkits/alu/alu_pkg.sv
new file mode 100644
index 0000000..41de7f3
--- /dev/null
+++ b/verif/vkits/alu/alu_pkg.sv
@@ -0,0 +1,36 @@
+
+// **********************************************************************
+// *
+// * legal mumbo jumbo
+// *
+// * (c) 2013
+// * (utg v1.0)
+// ***********************************************************************
+// File:   alu_pkg.sv
+// Author: bhunter
+/* About:  alu package
+ *************************************************************************/
+
+`include "uvm_macros.svh"
+
+// package: alu_pkg
+package alu_pkg;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Imports
+   import uvm_pkg::*;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Types
+
+   // type: result_t
+   // All ALU results are of this type
+	typedef logic [31:0] result_t;
+
+   //----------------------------------------------------------------------------------------
+   // Group: Includes
+`include "alu_item.sv"
+
+endpackage : alu_pkg
+
+
